/* 
 * The Maven coordinates for the project artifact
 */
description='The FireSat example project'
group = 'io.opencaesar.ontologies'
version = '1.4.0'

/* 
 * The Gradle task dependencies 
 */
buildscript {
	repositories {
		mavenLocal()
		maven { url 'https://dl.bintray.com/opencaesar/owl-adapter' }
		maven { url 'https://dl.bintray.com/opencaesar/owl-tools' }
		maven { url 'https://dl.bintray.com/opencaesar/oml-tools' }
		maven { url 'https://dl.bintray.com/opencaesar/oml' }
		maven { url 'https://dl.bintray.com/vermeulen-mp/gradle-plugins' }
		jcenter()
	}
	dependencies {
		classpath 'io.opencaesar.owl:owl-shacl-fuseki-gradle:+'
		classpath 'io.opencaesar.owl:owl-query-gradle:+'
		classpath 'io.opencaesar.owl:owl-load-gradle:+'
		classpath 'io.opencaesar.owl:owl-reason-gradle:+'
		classpath 'io.opencaesar.owl:oml2owl-gradle:+'
		classpath 'io.opencaesar.oml:oml-bikeshed-gradle:+'
		classpath 'io.opencaesar.oml:oml-merge-gradle:+'
		classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:+'
		classpath 'com.wiredforcode:gradle-spawn-plugin:+'
		// needed since gradle-bintray-plugin brings an old version of xerces
		configurations.classpath.exclude group: 'xerces', module: 'xercesImpl'
	}
}

/*
 * Dependency versions
 */
ext { 
	jplVocabulariesVersion = '1.+' 
	fusekiVersion = '3.16.0' 
}

/*
 * The configuration of OML dependencies
 */
configurations {
	oml
    fuseki
}

/*
 * The repositories to look up OML dependencies in
 */
repositories {
	mavenLocal()
	maven { url 'https://dl.bintray.com/opencaesar/jpl-vocabularies' }
	jcenter()
}

/*
 * The OML dependencies
 */
dependencies {
	oml "io.opencaesar.ontologies:jpl-vocabularies:$jplVocabulariesVersion"
    fuseki "org.apache.jena:jena-fuseki-server:$fusekiVersion"
}

/*
 * A task to extract and merge the OML dependencies
 */
task downloadDependencies(type:io.opencaesar.oml.merge.OmlMergeTask) {
    inputZipPaths = configurations.oml.files
    outputCatalogFolder = file('build/oml')
}

/*
 * A task to generate Bikeshed specification for the OML catalog
 */
task omlToBikeshed(type: io.opencaesar.oml.bikeshed.Oml2BikeshedTask, dependsOn: downloadDependencies) {
	// OML catalog
	inputCatalogPath = file('catalog.xml')
	// OWL folder
	outputFolderPath = file('build/bikeshed')
	// Input Ontology Iri
	rootOntologyIri = 'http://opencaesar.io/programs/earth-science/projects/firesat/bundle'
	// Publish URL
	publishUrl = 'https://opencaesar.github.io/firesat-example'
}

/*
 * A task to generate the model documentation in HTML
 */
import org.gradle.internal.os.OperatingSystem
task generateDocs(dependsOn: omlToBikeshed) {
	doLast {
		if (OperatingSystem.current().isWindows()) {
			exec { commandLine 'build/bikeshed/publish.bat' }
		} else {
			exec { commandLine 'chmod', '+x', 'build/bikeshed/publish.sh' }
			exec { commandLine 'build/bikeshed/publish.sh' }
		}
	}
}

/*
 * A task to convert the OML catalog to OWL catalog
 */
task omlToOwl(type:io.opencaesar.oml2owl.Oml2OwlTask, dependsOn: downloadDependencies) {
	// OML catalog
	inputCatalogPath = file('catalog.xml')
	// OWL catalog
	outputCatalogPath = file('build/owl/catalog.xml')
}

/*
 * A task to run the Openllet reasoner on the OWL catalog
 */
task owlReason(type:io.opencaesar.owl.reason.OwlReasonTask, dependsOn: omlToOwl) {
	// OWL catalog
	catalogPath = file('build/owl/catalog.xml')
	// Input ontology IRI to reason on
	inputOntologyIri = 'http://opencaesar.io/programs/earth-science/projects/firesat/bundle'
	// Entailment statements to generate and the ontologies to persist them in
	specs = [
		'http://opencaesar.io/programs/earth-science/projects/firesat/bundle/classes = ALL_SUBCLASS',
		'http://opencaesar.io/programs/earth-science/projects/firesat/bundle/properties = INVERSE_PROPERTY | ALL_SUBPROPERTY',
		'http://opencaesar.io/programs/earth-science/projects/firesat/bundle/individuals = ALL_INSTANCE | DATA_PROPERTY_VALUE | OBJECT_PROPERTY_VALUE | SAME_AS'
	]
	// Junit error report
	reportPath = file('build/reports/reasoning.xml')
}

/*
 * A task to load an OWL catalog to a Fuseki dataset endpoint
 */
task owlLoad(type:io.opencaesar.owl.load.OwlLoadTask, dependsOn: owlReason) {
	catalogPath = file('build/owl/catalog.xml')
	endpointURL = 'http://localhost:3030/firesat'
    fileExtensions = ['owl', 'ttl']
	iris = [
		'http://opencaesar.io/programs/earth-science/projects/firesat/bundle/classes',
	    'http://opencaesar.io/programs/earth-science/projects/firesat/bundle/properties',
	    'http://opencaesar.io/programs/earth-science/projects/firesat/bundle/individuals'
	]
}

/*
 * A task to run a set of SPARQL queries on a Fuseki dataset endpoint
 */
task owlQuery(type:io.opencaesar.owl.query.OwlQueryTask, dependsOn: owlLoad) {
	endpointURL = 'http://localhost:3030/firesat'
	queryPath = file('src/sparql')
	resultPath = file('build/frames')
}

/*
 * A task to run a set of SHACL validation rules on a Fuseki dataset endpoint
 */
task owlShacl(type:io.opencaesar.owl.shacl.fuseki.OwlShaclFusekiTask, dependsOn: owlLoad) {
	endpointURL = 'http://localhost:3030/firesat'
	queryPath = file('src/shacl')
	resultPath = file('build/reports')
}

/*
 * A task to build the project, which executes several tasks together
 */
task build() {
	dependsOn owlReason
}

/*
 * A task to delete the build artifacts
 */
task clean(type: Delete) {
	delete 'build'
}

/*
 * Publish to Maven spec
 */
apply plugin: 'maven-publish'

task omlZip(type: Zip) {
	from file('src/oml')
	include "**/*.oml"
	destinationDirectory = file('build/libs')
	archiveBaseName = project.name
	archiveVersion = project.version
}

publishToMavenLocal.dependsOn omlZip

publishing {
    publications {
        maven(MavenPublication) {
            artifact omlZip
			pom.withXml {
                def dependencies = asNode().appendNode('dependencies')
                configurations.oml.allDependencies.stream().each {
                    def dependency = dependencies.appendNode('dependency')
                    dependency.appendNode('groupId', it.group)
                    dependency.appendNode('artifactId', it.name)
                    dependency.appendNode('version', it.version)
                }
            }
        }
    }
}

/*
 * Publish to Bintrary Spec
 */
apply plugin: 'com.jfrog.bintray'

ext.gh = [
	dep: 'https://github.com',
	org: 'opencaesar',
	rep: 'firesat-example'
]

bintray {
	user = System.getenv('BINTRAY_USER')
	key = System.getenv('BINTRAY_API_KEY')
	publications = ['maven']
	publish = true
	override = true
	pkg {
		name = project.name
		repo = gh.rep
		userOrg = gh.org
		desc = project.description
		licenses = ['Apache-2.0']
		websiteUrl = gh.dep+'/'+gh.org+'/'+gh.rep
        issueTrackerUrl = gh.dep+'/'+gh.org+'/'+gh.rep+'/issues'
		vcsUrl = gh.dep+'/'+gh.org+'/'+gh.rep+'.git'
		version {
			name = project.version
			desc = project.version
			released  = new Date()
		}
	}

}

/*
 * Start and stop the Fuseki server
 */
apply plugin: 'com.wiredforcode.spawn'

task downloadFuseki(type: Copy) {
    from configurations.fuseki
    into file("build/libs")
}

task startFuseki(type: SpawnProcessTask, dependsOn: downloadFuseki) {
	command "java -jar ${projectDir}/build/libs/jena-fuseki-server-${fusekiVersion}.jar --config=${projectDir}/.fuseki.ttl"
	ready "Apache Jena Fuseki ${fusekiVersion}"
}

task stopFuseki(type: KillProcessTask)
