/* 
 * The Maven coordinates for the project artifact
 */
ext.title = "FireSat Example"
description='The FireSat example project'
group = 'io.opencaesar.ontologies'
version = '3.1.0'

apply from: "${rootDir}/gradle/maven-deployment.gradle"

/* 
 * The Gradle task dependencies 
 */
buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
    }
    dependencies {
        classpath 'io.opencaesar.owl:owl-fuseki-gradle:+'
        classpath 'io.opencaesar.owl:owl-shacl-fuseki-gradle:+'
        classpath 'io.opencaesar.owl:owl-query-gradle:+'
        classpath 'io.opencaesar.owl:owl-load-gradle:+'
        classpath 'io.opencaesar.owl:owl-reason-gradle:+'
        classpath 'io.opencaesar.oml:oml-bikeshed-gradle:1.+'
        classpath 'io.opencaesar.oml:oml-merge-gradle:1.+'
        classpath 'io.opencaesar.adapters:oml2owl-gradle:1.+'
    }
}

/*
 * Bikeshed folder
 */
ext {
	bikeshed = 'build/bikeshed'
}

/*
 * Dependency versions
 */
ext { 
    imceVersion = '3.+'
}

/*
 * The configuration of OML dependencies
 */
configurations {
    oml
}

/*
 * The repositories to look up OML dependencies in
 */
repositories {
    mavenLocal()
    mavenCentral()
}

/*
 * The OML dependencies
 */
dependencies {
    oml "io.opencaesar.ontologies:imce-vocabularies:$imceVersion"
}

/*
 * A task to extract and merge the OML dependencies
 */
task downloadDependencies(type:io.opencaesar.oml.merge.OmlMergeTask) {
    inputZipPaths = configurations.oml.files
    outputCatalogFolder = file('build/oml')
}

/*
 * A task to generate Bikeshed specification for the OML catalog
 */
task omlToBikeshed(type: io.opencaesar.oml.bikeshed.Oml2BikeshedTask, dependsOn: downloadDependencies) {
    // OML catalog
    inputCatalogPath = file('catalog.xml')
    // OML catalog title
    inputCatalogTitle = project.title
    // OML catalog version
    inputCatalogVersion = project.version
    // Input Ontology Iri
    rootOntologyIri = 'http://opencaesar.io/examples/firesat/bundle'
    // OWL folder
    outputFolderPath = file("$bikeshed")
    // Publish URL
    publishUrl = 'https://opencaesar.github.io/firesat-example'
}

/*
 * A task to generate the model documentation in HTML
 */
import org.gradle.internal.os.OperatingSystem
task generateDocs(dependsOn: omlToBikeshed) {
	inputs.files(fileTree("$bikeshed").include('**/*.bs'))
	outputs.files(fileTree("$bikeshed").include('**/*.html'))
    doLast {
        if (OperatingSystem.current().isWindows()) {
            exec { commandLine "$bikeshed/publish.bat" }
        } else {
            exec { commandLine "$bikeshed/publish.sh" }
        }
    }
}

/*
 * A task to convert the OML catalog to OWL catalog
 */
task omlToOwl(type:io.opencaesar.oml2owl.Oml2OwlTask, dependsOn: downloadDependencies) {
    // OML catalog
    inputCatalogPath = file('catalog.xml')
    // OWL catalog
    outputCatalogPath = file('build/owl/catalog.xml')
}

/*
 * A task to run the Openllet reasoner on the OWL catalog
 */
task owlReason(type:io.opencaesar.owl.reason.OwlReasonTask, dependsOn: omlToOwl) {
    // OWL catalog
    catalogPath = file('build/owl/catalog.xml')
    // Input ontology IRI to reason on
    inputOntologyIri = 'http://opencaesar.io/examples/firesat/bundle'
    // Entailment statements to generate and the ontologies to persist them in
    specs = [
        'http://opencaesar.io/examples/firesat/bundle/classes = ALL_SUBCLASS',
        'http://opencaesar.io/examples/firesat/bundle/properties = INVERSE_PROPERTY | ALL_SUBPROPERTY',
        'http://opencaesar.io/examples/firesat/bundle/individuals = ALL_INSTANCE | DATA_PROPERTY_VALUE | OBJECT_PROPERTY_VALUE | SAME_AS'
    ]
    // Junit error report
    reportPath = file('build/reports/reasoning.xml')
}

/*
 * Start and stop the Fuseki server
 */
task startFuseki(type: io.opencaesar.owl.fuseki.StartFusekiTask) {
    configurationPath = file('.fuseki.ttl')
    outputFolderPath = file('.fuseki')
}

task stopFuseki(type: io.opencaesar.owl.fuseki.StopFusekiTask) {
    outputFolderPath = file('.fuseki')
}

/*
 * A task to load an OWL catalog to a Fuseki dataset endpoint
 */
task owlLoad(type:io.opencaesar.owl.load.OwlLoadTask, dependsOn: owlReason) {
    catalogPath = file('build/owl/catalog.xml')
    endpointURL = 'http://localhost:3030/firesat'
    fileExtensions = ['owl', 'ttl']
    iris = [
        'http://opencaesar.io/examples/firesat/bundle/classes',
        'http://opencaesar.io/examples/firesat/bundle/properties',
        'http://opencaesar.io/examples/firesat/bundle/individuals'
    ]
}

/*
 * A task to run a set of SPARQL queries on a Fuseki dataset endpoint
 */
task owlQuery(type:io.opencaesar.owl.query.OwlQueryTask, dependsOn: owlLoad) {
	inputs.files(owlLoad.inputs.files) // rerun when the dataset changes
    endpointURL = 'http://localhost:3030/firesat'
    queryPath = file('src/sparql')
    resultPath = file('build/results')
}

/*
 * A task to run a set of SHACL validation rules on a Fuseki dataset endpoint
 */
task owlShacl(type:io.opencaesar.owl.shacl.fuseki.OwlShaclFusekiTask, dependsOn: owlLoad) {
    endpointURL = 'http://localhost:3030/firesat'
    queryPath = file('src/shacl')
    resultPath = file('build/reports')
}

/*
 * A task to build the project, which executes several tasks together
 */
tasks.named('build') {
    dependsOn owlReason
}

/*
 * Publish artifacts
 */

task omlZip(type: Zip) {
    from file('src/oml')
    include "**/*.oml"
    destinationDirectory = file('build/libs')
    archiveBaseName = project.name
    archiveVersion = project.version
}

publishing.publications.maven.artifact omlZip

task vocabularyZip(type: Zip) {
    from file('src/oml')
    include "imce.jpl.nasa.gov/**/*.oml"
    destinationDirectory = file('build/libs')
    archiveBaseName = project.name
    archiveVersion = project.version
    archiveClassifier = 'vocabularies'
}

publishing.publications.maven.artifact vocabularyZip

/*
 * Integration with the Eclipse IDE
 */ 
apply plugin: 'eclipse'

eclipse {
    synchronizationTasks downloadDependencies
}